// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
NotDroppable is ItemEnchantment

constants:
   include blakston.khd

resources:
   
   NotDroppable_name_rsc = "Not Droppable"
   NotDroppable_icon_rsc = light.bgf
   NotDroppable_desc_rsc = \
      "Makes a item un-droppable."

   NotDroppable_cast  = "You make the %s not droppable."
   NotDroppable_fails = "You can't cast not droppable on %s%s."

classvars:

   vrName = NotDroppable_name_rsc
   vrIcon = NotDroppable_icon_rsc
   vrDesc = NotDroppable_desc_rsc

   viSpell_num = SID_NODROP
   viSchool = SS_DM_COMMAND

   viSpell_level = 3
   viMana = 10
   viSpellExertion = 3
   viCast_time = 0

   viChance_To_Increase = 25
   viMeditate_ratio = 30

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Mushroom,5],plReagents);
      plReagents = Cons([&Elderberry,5],plReagents);

      return;
   }

   CanPayCosts(who=$,lTargets=$)
   {
      local oWeapon, oTarget;

      if lTargets = $
      {
         return FALSE;
      }

      oTarget = First(lTargets);

      // Check that target is an item
      if NOT IsClass(oTarget,&PassiveItem)
      OR IsClass(oTarget,&NumberItem)
      OR IsClass(oTarget,&SpellItem)
      {
         Send(who,@MsgSendUser,#message_rsc=NotDroppable_fails,
               #parm1=Send(oTarget,@GetCapDef),#parm2=Send(oTarget,@GetName));

        return FALSE;
      }

      propagate;
   }

   CastSpell(who=$,lTargets=$,iSpellPower=0)
   {
      local iCurrentSpell, oItem, oItemAtt;

      oItem = First(lTargets);

      oItemAtt = Send(SYS,@FindItemAttByNum,#num=IA_NOTDROPPABLE);
      iCurrentSpell = Send(oItem,@GetAttributeData,#ItemAtt=IA_NOTDROPPABLE);
      
      Send(oItemAtt,@AddtoItem,#oItem=oItem);

      Send(who,@MsgSendUser,#message_rsc=NotDroppable_cast,
            #parm1=Send(oItem,@GetName));
      
      Send(oItem,@SetNotDroppable);

      propagate;
   }

end
////////////////////////////////////////////////////////////////////////////////