// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
IvyCirclet is Helmet

constants:

   include blakston.khd

resources:

   include ivycircl.lkod

   IvyCirclet_name_rsc = "ivy circlet"
   IvyCirclet_icon_rsc = ivycircl.bgf
   IvyCirclet_desc_rsc = \
      "Healthy, green leaves of ivy are wrapped around a wooden wreath.  "
      "This looks like it would be cool and refreshing against your brow."

   ivycirclet_cant = \
      "The ivy circlet stubbornly resists your attempts to place it on your brow."

   ivy_not_shal = "Only a follower of Shal'ille can wear this circlet."

   IvyCirclet_overlay_rsc = IvyCirov.bgf

classvars:

   vrName = IvyCirclet_name_rsc
   vrIcon = IvyCirclet_icon_rsc
   vrDesc = IvyCirclet_desc_rsc

   viHits_init_min = 255
   viHits_init_max = 300

   viValue_average = 450
   viWeight = 10
   viBulk = 20

   viUse_type = ITEM_USE_HEAD
   viUse_amount = 1

   viGround_group = 1
   viInventory_group = 2

   // This is the default value for piDefense_bonus
   viDefense_base = 10

   viLayer = 2

properties:

   piMana_change = 0
   piHealth_change = 0

messages:

   GetOverlay(animation = $)
   {
      // Uses different file for the overlay.
      return IvyCirclet_overlay_rsc;
   }

   GetResistanceModifiers()
   {
      return [ [-ATCK_SPELL_FIRE,-10],
               [-ATCK_SPELL_UNHOLY,-15],
               [-ATCK_SPELL_HOLY,15]
             ];
   }

   ReqUse()
   {
      local i, oSpell, oFound;

      if Send(poOwner,@PlayerIsImmortal)
         OR IsClass(poOwner,&EscapedConvict)
      {
         return TRUE;
      }

      oFound = FALSE;

      if poOwner <> $
      {
         foreach i in Send(poOwner,@GetSpellList)
         {
            oSpell = Send(SYS,@FindSpellByNum,
                           #num=Send(poOwner,@DecodeSpellNum,#compound=i));
            if Send(oSpell,@GetSchool) = SS_SHALILLE
            {
               oFound = TRUE;

               break;
            }
         }

         if oFound = FALSE
         {
            Send(poOwner,@MsgSendUser,#message_rsc=ivy_not_shal);
            
            return FALSE;
         }
		}

      propagate;
   }

   GetBaseSpellModifier(oSpell=$)
   {
      if Send(oSpell,@GetSchool) = SS_SHALILLE
      {
         return 15;
      }

      return 0;
   }

end
////////////////////////////////////////////////////////////////////////////////
